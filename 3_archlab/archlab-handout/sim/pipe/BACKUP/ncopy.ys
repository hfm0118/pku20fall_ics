#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Include your name and ID here.
# 1900013010 Hu F.M.
# Describe how and why you modified the baseline code.
#
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion
	# Loop header
#	xorq %rax, %rax
	jmp Header

# operate 8 nums
Loop:
	mrmovq (%rdi), %rbx	# read val from src...
	mrmovq $0x8(%rdi), %rcx
	mrmovq $0x10(%rdi), %r8
	mrmovq $0x18(%rdi), %r9
	mrmovq $0x20(%rdi), %r10
	mrmovq $0x28(%rdi), %r11
	mrmovq $0x30(%rdi), %r12
	mrmovq $0x38(%rdi), %r13

	andq %rbx, %rbx
	rmmovq %rbx, (%rsi)
	jle L1
	iaddq $1, %rax
L1:	andq %rcx, %rcx
	rmmovq %rcx, $0x8(%rsi)
	jle L2
	iaddq $1, %rax
L2:	andq %r8, %r8
	rmmovq %r8, $0x10(%rsi)
	jle L3
	iaddq $1, %rax	
L3:	andq %r9, %r9
	rmmovq %r9, $0x18(%rsi)
	jle L4
	iaddq $1, %rax
L4:	andq %r10, %r10
	rmmovq %r10, $0x20(%rsi)
	jle L5
	iaddq $1, %rax
L5:	andq %r11, %r11
	rmmovq %r11, $0x28(%rsi)
	jle L6
	iaddq $1, %rax
L6:	andq %r12, %r12
	rmmovq %r12, $0x30(%rsi)
	jle L7
	iaddq $1, %rax
L7:	andq %r13, %r13
	rmmovq %r13, $0x38(%rsi)
	jle L8
	iaddq $1, %rax
L8:
	iaddq $64, %rdi		# src++
	iaddq $64, %rsi		# dst++
Ltest:	iaddq $-8, %rdx		# len >= 8?
	jge Loop		# if so, goto Loop:

	iaddq $8, %rdx
	jmp Btest
LoopB:
	mrmovq (%rdi), %rbx	# read val from src...
	mrmovq $0x8(%rdi), %rcx	

	andq %rbx, %rbx
	rmmovq %rbx, (%rsi)
	jle LB1
	iaddq $1, %rax
LB1:	andq %rcx, %rcx
	rmmovq %rcx, $0x8(%rsi)	
	jle LB2
	iaddq $1, %rax
LB2:	iaddq $16, %rdi		# src++
	iaddq $16, %rsi		# dst++
Btest:	iaddq $-2, %rdx		# len >= 2?
	jge LoopB

	iaddq $2, %rdx
	jle Done
	mrmovq (%rdi), %rbx
	andq %rbx, %rbx
	rmmovq %rbx, (%rsi)
	jle Done
	iaddq $1, %rax
	jmp Done

Header:	andq %rdx, %rdx
	jg Ltest

##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
