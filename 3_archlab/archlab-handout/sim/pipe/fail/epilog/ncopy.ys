#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Include your name and ID here.
# 1900013010 Hu F.M.
# Describe how and why you modified the baseline code.
#
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion
	# Loop header
#	xorq %rax, %rax
	andq %rdx, %rdx
	jle Done
	jmp Ltest

# operate 8 nums
Loop:
	mrmovq (%rdi), %rbx	# read val from src...
	mrmovq $0x8(%rdi), %rcx
	mrmovq $0x10(%rdi), %r8
	mrmovq $0x18(%rdi), %r9
	mrmovq $0x20(%rdi), %r10
	mrmovq $0x28(%rdi), %r11
	mrmovq $0x30(%rdi), %r12
	mrmovq $0x38(%rdi), %r13

	rmmovq %rbx, (%rsi)	# ...and store it to dst
	rmmovq %rcx, $0x8(%rsi)
	rmmovq %r8, $0x10(%rsi)
	rmmovq %r9, $0x18(%rsi)
	rmmovq %r10, $0x20(%rsi)
	rmmovq %r11, $0x28(%rsi)
	rmmovq %r12, $0x30(%rsi)
	rmmovq %r13, $0x38(%rsi)

	andq %rbx, %rbx
	jle L1
	iaddq $1, %rax
L1:	andq %rcx, %rcx
	jle L2
	iaddq $1, %rax
L2:	andq %r8, %r8
	jle L3
	iaddq $1, %rax	
L3:	andq %r9, %r9
	jle L4
	iaddq $1, %rax
L4:	andq %r10, %r10
	jle L5
	iaddq $1, %rax
L5:	andq %r11, %r11
	jle L6
	iaddq $1, %rax
L6:	andq %r12, %r12
	jle L7
	iaddq $1, %rax
L7:	andq %r13, %r13
	jle L8
	iaddq $1, %rax
L8:
	iaddq $64, %rdi		# src++
	iaddq $64, %rsi		# dst++
Ltest:	iaddq $-8, %rdx		# len >= 8?
	jge Loop		# if so, goto Loop:

	iaddq $8, %rdx
	andq %rdx, %rdx
	jle Done
	mrmovq (%rdi), %rbx
	mrmovq $0x8(%rdi), %rcx
	rmmovq %rbx, (%rsi)
	andq %rbx, %rbx
	jle E1
	iaddq $1, %rax
E1:	iaddq $-1, %rdx
	jle Done
	rmmovq %rcx, $0x8(%rsi)
	andq %rcx, %rcx
	jle E2
	iaddq $1, %rax
E2:	iaddq $-1, %rdx
	jle Done
	mrmovq $0x10(%rdi), %rbx
	mrmovq $0x18(%rdi), %rcx
	rmmovq %rbx, $0x10(%rsi)
	andq %rbx, %rbx
	jle E3
	iaddq $1, %rax
E3:	iaddq $-1, %rdx
	jle Done
	rmmovq %rcx, $0x18(%rsi)
	andq %rcx, %rcx
	jle E4
	iaddq $1, %rax
E4:	iaddq $-1, %rdx
	jle Done
	mrmovq $0x20(%rdi), %rbx
	mrmovq $0x28(%rdi), %rcx
	rmmovq %rbx, $0x20(%rsi)
	andq %rbx, %rbx
	jle E5
	iaddq $1, %rax
E5:	iaddq $-1, %rdx
	jle Done
	rmmovq %rcx, $0x28(%rsi)
	andq %rcx, %rcx
	jle E6
	iaddq $1, %rax
E6:	iaddq $-1, %rdx
	jle Done
	mrmovq $0x30(%rdi), %rbx
	rmmovq %rbx, $0x30(%rsi)
	andq %rbx, %rbx
	jle Done
	iaddq $1, %rax


##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
